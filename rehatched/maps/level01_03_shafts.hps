//////////////////////
/*CALLBACK FUNCTIONS*/


//////////////////
//START ENTRANCE//
void EntranceInteract01(string asEntity)
{
	CreateTimer("entrance1", 0.15f, "Entrance01", false);
	PlayGuiSound("interact_locked_metal_door", 0.8f);
}

void Entrance01(string asTimer)
{	
	AddMessageTrans("01_03_shafts", "EventEntrance01");
}

void Shake01(string asTimer)
{	
	FadeInSoundEntity("rumble", 8.5f);
	StartScreenShake(0.02f, 2.5f,0,0);
	CreateTimer("shake2", 1.2f, "Shake02", false);
}

void Shake02(string asTimer)
{
	AddNotebookTask("01_03_entrance_closed", "PersonalNotes", "01_03_entrance_closed");
	FadeOutSoundEntity("rumble", 0.5f);
}
//STOP ENTRANCE//
/////////////////


/////////////////////
//START LOCKED DOOR//
void ShaftDoorUnlock01(string asItem, string asEntity)
{
	SetDoorLocked("shaftdoor1", false);
	CreateSoundEntityAt("Entity","unlock","unlock1","key_unlock.snt");
	PlayGuiSound("horror_deep_thump", 0.5f);
	RemoveEntityCallback("PlayerInteract","shaftdoor1");
	RemoveNotebookTask("01_03_need_key");
	RemoveItem("oldminekey");
	AutoSave();
}

void ShaftDoorInteract01(string asEntity)
{
	CreateTimer("delayshaft1", 0.2f, "DelayShaft01", false);
	PlayGuiSound("interact_closed_door_old", 0.6f);
	AddNotebookTask("01_03_need_key", "PersonalNotes", "01_03_need_key");
	RemoveEntityCallback("PlayerInteract","shaftdoor1");
}

void DelayShaft01(string asTimer)
{
	AddSubTitleTrans("01_03_shafts", "EventShaftDoor01", 1.5f);
}
//STOP LOCKED DOOR//
////////////////////


//////////////////////////
//START DOG INTRODUCTION//
/*This happens if player visits OFFICE first
 */
void Dog01(string asParent, string asChild)
{
	PlaySoundEntity("howl1",false);
	
	PlayGameMusic("penumbra_music_E1_C1", 1,0.2f,false, 7);
	
	SetGameEntityActive("dog01",true);
	SetLocalVar("whatdog", 1);
	
	/*Disable areas for dog, see START DOG DISABLE AND ENABLE
	 */
	//AddSubTitle("DogArea Active",3);
	SetLocalVar("dogdisable", 1);
	SetGameEntityActive("triggerdog",false);
	CreateTimer("dogscene1", 0.2f, "DogScene01", false);
}

void DogScene01(string asTimer)
{
	SetPlayerActive(false);
	SetWideScreenActive(true);
	
	StartPlayerLookAt("lookdog1",4, 1);
	
	CreateTimer("dogscene2", 0.3f, "DogScene02", false);
}

void DogScene02(string asTimer)
{	
	AddSubTitleTrans("01_03_shafts", "EventDog01", 1.9f);
	CreateTimer("dogscene3", 1.9f, "DogScene03", false);
}

void DogScene03(string asTimer)
{
	StartPlayerLookAt("lookdog2",1, 4);
	AddSubTitleTrans("01_03_shafts", "EventDog02", 3);
	CreateTimer("dogscene4", 1.5f, "DogScene04", false);
}

void DogScene04(string asTimer)
{
	StopPlayerLookAt();
	SetWideScreenActive(false);
	SetPlayerActive(true);
	
	CreateTimer("Sneak3", 13, "SneakTutorial03",false);
}
//STOP DOG INTRODUCTION//
/////////////////////////


/////////////////////////////////
//START DOG INTRODUCTION SECOND//
/*This happens if player visits STORAGE first
 */
void Dog201(string asParent, string asChild)
{
	PlaySoundEntity("howl2",false);
	
	PlayGameMusic("penumbra_music_E1_C1", 1,0.2f,false, 7);
	
	SetGameEntityActive("dog02",true);
	SetLocalVar("whatdog", 2);
	
	/*Disable areas for dog, see START DOG DISABLE AND ENABLE
	 */
	//AddSubTitle("DogArea Active",3);
	SetLocalVar("dogdisable", 1);
	
	SetGameEntityActive("triggerdog2",false);
		
	CreateTimer("dogscene21", 0.2f, "DogScene201", false);
}
void DogScene201(string asTimer)
{
	SetPlayerActive(false);
	SetWideScreenActive(true);
	
	StartPlayerLookAt("lookdog3",4, 1);
	
	CreateTimer("dogscene22", 0.3f, "DogScene202", false);
}
void DogScene202(string asTimer)
{	
	AddSubTitleTrans("01_03_shafts", "EventDog01", 1.9f);
	
	CreateTimer("dogscene23", 1.9f, "DogScene203", false);
}
void DogScene203(string asTimer)
{
	StartPlayerLookAt("lookdog4",1, 4);
	
	AddSubTitleTrans("01_03_shafts", "EventDog02", 3);
	
	CreateTimer("dogscene24", 1.5f, "DogScene204", false);
}
void DogScene204(string asTimer)
{
	StopPlayerLookAt();
	SetWideScreenActive(false);
	SetPlayerActive(true);
	
	CreateTimer("Sneak3", 13, "SneakTutorial03",false);
}
//STOP DOG INTRODUCTION SECOND//
////////////////////////////////


//////////////////
//START MOVE DOG//
void MoveDog01North(string asParent, string asChild)
{
	if(GetLocalVar("movedogcheck") == 0)
	{
		if(GetLocalVar("whatdog") == 1)
			SetGameEntityActive("dog01",true);
		if(GetLocalVar("whatdog") == 2)
			SetGameEntityActive("dog02",true);
			
		for(int i=1;i<=8;i++)
			DestroyTimer("dogscares"+i);
		
		if(GetLocalVar("dogmessage") == 0)
		{
			AddSubTitleTrans("01_03_shafts", "EventDogComes", 4.2f);
			
			CreateSoundEntityAt("Entity","dognoise3","dogsound8","enemy_dog_scare_howl");
			
			SetLocalVar("dogmessage", 1);
		}
		
		ClearEnemyPatrolNodes("dog01");
		
		AddEnemyPatrolNode("dog01","Node80",9,"");
		AddEnemyPatrolNode("dog01","Node262",9,"");
		AddEnemyPatrolNode("dog01","Node267",9,"");
		AddEnemyPatrolNode("dog01","Node101",9,"");
		AddEnemyPatrolNode("dog01","Node263",3,"");
		AddEnemyPatrolNode("dog01","Node294",3,"");
		AddEnemyPatrolNode("dog01","Node85",9,"");
		AddEnemyPatrolNode("dog01","Node81",5,"");
		
	
		ClearEnemyPatrolNodes("dog02");
		
		AddEnemyPatrolNode("dog02","Node80",9,"");
		AddEnemyPatrolNode("dog02","Node262",9,"");
		AddEnemyPatrolNode("dog02","Node267",9,"");
		AddEnemyPatrolNode("dog02","Node101",9,"");
		AddEnemyPatrolNode("dog02","Node263",3,"");
		AddEnemyPatrolNode("dog02","Node294",3,"");
		AddEnemyPatrolNode("dog02","Node85",9,"");
		AddEnemyPatrolNode("dog02","Node81",5,"");
		
		SetLocalVar("movedogcheck", 1);
		
		//AddSubTitle("Changed to north path!\n",2.0);
	}
	
}

void MoveDog01South(string asParent, string asChild)
{
	if(GetLocalVar("movedogcheck") == 1)
	{
		if(GetLocalVar("whatdog") == 1)
			SetGameEntityActive("dog01",true);
		if(GetLocalVar("whatdog") == 2)
			SetGameEntityActive("dog02",true);
		
		ClearEnemyPatrolNodes("dog01");
		
		AddEnemyPatrolNode("dog01","Node117",2,"");
		AddEnemyPatrolNode("dog01","Node115",3,"");
		AddEnemyPatrolNode("dog01","Node31",1,"");
		AddEnemyPatrolNode("dog01","Node29",3,"");
		AddEnemyPatrolNode("dog01","Node35",2,"");
		AddEnemyPatrolNode("dog01","Node2",4,"");
		AddEnemyPatrolNode("dog01","Node37",3,"");
		AddEnemyPatrolNode("dog01","Node34",2,"");
		AddEnemyPatrolNode("dog01","Node26",4,"");
	
		ClearEnemyPatrolNodes("dog02");
		
		AddEnemyPatrolNode("dog02","Node117",2,"");
		AddEnemyPatrolNode("dog02","Node115",3,"");
		AddEnemyPatrolNode("dog02","Node31",1,"");
		AddEnemyPatrolNode("dog02","Node29",3,"");
		AddEnemyPatrolNode("dog02","Node35",2,"");
		AddEnemyPatrolNode("dog02","Node2",4,"");
		AddEnemyPatrolNode("dog02","Node37",3,"");
		AddEnemyPatrolNode("dog02","Node34",2,"");
		AddEnemyPatrolNode("dog02","Node26",4,"");
		
		SetLocalVar("movedogcheck", 0);
		
		//AddSubTitle("Changed to south path!\n",2.0);
	}	
}

void DogDoorBreak(string asEntity)
{
	CreateSoundEntityAt("Entity","unlock","dogdoor","enemy_dog_angry_amb_long.snt");
}
//STOP MOVE DOG//
/////////////////


///////////////////
//START BLOW HOLE//

//-- PLAYER EXAMINES TNT
void ExamineTNT(string asEntity)
{
	/*Only if player found exit
	 */
	if(GetLocalVar("foundwayout") == 1)
		AddNotebookTask("01_03_tnt_need_fuse", "PersonalNotes", "01_03_tnt_need_fuse");		
}

//-- TOO EARLY STUFF START
void FuseOnBarrel01(string asItem, string asEntity)
{
	AddSubTitleTrans("01_03_shafts", "EventFuseOnBarrel01", 3);
}
void LightFuse01(string asItem, string asEntity)
{
	AddSubTitleTrans("01_03_shafts", "EventZippoOnBarrel", 3);
}
void StringOnBarrel01(string asItem, string asEntity)
{
	AddSubTitleTrans("01_03_shafts", "EventStringOnBarrel", 3);
}
void SoakedStringOnBarrel01(string asItem, string asEntity)
{
	AddSubTitleTrans("01_03_shafts", "EventSoakedStringOnBarrel", 3);
}
//-- TOO EARLY STUFF STOP

//Placing barrel in hole
void OnAttach(string asArea, string asBody)
{
	if(StringContains(asBody, "tnt"))
	{
		AllowAttachment();
		RemoveUseCallback("FuseOnBarrel01");
		AddUseCallback("fuse", "tnt1", "FuseOnBarrel02");
		CreateParticleSystem("spark1dust", "particle_stick_rock_dust", "spark1area",1,1,1);
	}
}	
void TntInHole01(string asParent, string asChild)
{
	CreateSoundEntityAt("Entity","spark1area","barrelplace","mine_event_barrel_place");
	
	SetGameEntityMaxInteractDist("fuse",2.7f);
	SetGameEntityMaxInteractDist("cottonstring",2.7f);
	SetGameEntityMaxInteractDist("zippofire",2.7f);
	SetGameEntityMaxInteractDist("tnt1",2.7f);
	SetGameEntityMaxInteractDist("tnt2",2.7f);
}
/*void TntOutHole01(string asParent, string asChild)
{
	AddUseCallback("fuse", "tnt1", "FuseOnBarrel01");
	RemoveUseCallback("FuseOnBarrel02");
}*/
//Adding fuse to barrel
void FuseOnBarrel02(string asItem, string asEntity)
{
	//FadeOut(0.5f);
	
	//SetPlayerActive(false);
	PlayGuiSound("mine_event_fuse_place", 0.65f);
	
	RemoveNotebookTask("01_03_tnt_need_fuse");	
	
	CreateTimer("fusescene1", 0.5f, "FuseScene01", false);
}
void FuseScene01(string asTimer)
{
	AddUseCallback("zippofire", "tnt2", "LightFuse02");
	
	SetGameEntityActive("tnt1",false);
	SetGameEntityActive("tnt2",true);
	
	RemoveItem("fuse");
	
	RemoveUseCallback("FuseOnBarrel02");
	RemoveUseCallback("LightFuse01");
	
	//CreateTimer("fusescene2", 4.3f, "FuseScene02", false);
}
void FuseScene02(string asTimer)
{
	FadeIn(1);
	
	SetPlayerActive(true);
}
//Fuse burn
void LightFuse02(string asItem, string asEntity)
{
	PlayGuiSound("lighter_burst", 1);
	
	CreateParticleSystem("spark1", "particle_spark", "spark1area",1,1,1);
	CreateSoundEntityAt("Entity","spark1area","fuse1","item_fuse_loop");
	AddSubTitleTrans("01_03_shafts", "EventTakeCover", 2.5f);
	CreateTimer("blow1", 1.8f, "BlowCountDown01", false);
	RemoveNotebookTask("01_03_found_exit");
}
void BlowCountDown01(string asTimer)
{
	CreateParticleSystem("spark2", "particle_spark", "spark2area",1,1,1);
	KillParticleSystem("spark1");
	
	CreateTimer("blow2", 1.9f, "BlowCountDown02", false);
}
void BlowCountDown02(string asTimer)
{
	CreateParticleSystem("spark3", "particle_spark", "spark3area",1,1,1);
	KillParticleSystem("spark2");
	
	CreateSoundEntityAt("Entity","spark3area","fuse2","item_fuse_loop");
	
	CreateTimer("blow3", 1.4f, "BlowCountDown03", false);
}
void BlowCountDown03(string asTimer)
{
	CreateParticleSystem("spark4", "particle_spark", "spark4area",1,1,1);
	KillParticleSystem("spark3");
	
	CreateTimer("blow4", 1.8f, "BlowCountDown04", false);
}
void BlowCountDown04(string asTimer)
{
	CreateParticleSystem("spark5", "particle_spark", "spark5area",1,1,1);
	KillParticleSystem("spark4");
	
	CreateSoundEntityAt("Entity","spark4area","fuse3","item_fuse_loop");
	
	CreateTimer("blow5", 1.6f, "BlowCountDown05", false);
}
void BlowCountDown05(string asTimer)
{
	KillParticleSystem("spark5");
	
	CreateParticleSystem("explo1", "particle_expl_tnt", "spark1area",1,1,1);
	
	CreateSoundEntityAt("Entity","spark3area","explosion1","explosion_tnt");
	
	StartScreenShake(0.02f, 0.7f,0,0);
	
	StopSoundEntity("fuse1",false);
	StopSoundEntity("fuse2",false);
	StopSoundEntity("fuse3",false);
	
	for(int i=1;i<=8;i++)
	{
		SetGameEntityActive("rock"+i,true);
		AddBodyImpulse("rock"+i,"world",0+i,0.5*i,4*i);
	}
	
	SetGameEntityActive("dynamitedeath",true);
	SetGameEntityActive("cave1",false);
	SetGameEntityActive("tnt2",false);
	SetGameEntityActive("exitarea",true);
	
	CreateSplashDamage("dynamitedeath",8,60,150,0,0,0,8);
	
	CreateLightFlashAtArea("dynamitedeath",7, 
				1,1,1,1,
				0.3, 0.5);
	
	CreateTimer("death1", 0.1f, "DeathCount01", false);
}
void DeathCount01(string asTimer)
{	
	SetGameEntityActive("dynamitedeath",false);
}
//STOP BLOW HOLE//
//////////////////


//////////////
//START EXIT//
void ExitInteract01(string asEntity)
{
	FadeOut(0.5f);
	//SetPlayerActive(false);
	StopGameMusic(0.2f, 0);
	StopSoundEntity("amb1",false);
	
	//PlayGuiSound("mine_event_crawl_exit", 1);
	
	CreateTimer("endlevel1", 0.1f, "EndLevel01", false);
	
	RemoveEntityCollideCallback("Enter","Player","exitarea");
}
void ExitEnter01(string asParent, string asChild)
{
	FadeOut(0.5f);
	//SetPlayerActive(false);
	StopGameMusic(0.2f, 0);
	StopSoundEntity("amb1",false);
	
	//PlayGuiSound("mine_event_crawl_exit", 1);
	
	CreateTimer("endlevel1", 0.1f, "EndLevel01", false);
	
	RemoveEntityCallback("PlayerInteract","exitarea");
}
void EndLevel01(string asTimer)
{
	//SetPlayerActive(true);
	
	ChangeMap(	"level01_07_lead_mine_shafts_A.dae", //New map
			"link1", //Positon on new map
			"mine_event_crawl_exit","", //Stop and end sound.
			0.1f , 0.5f,//Fade out and in time (seconds).
			"LoadTexts", "01_03_to_01_07"); //Load text (cat and entry)
}
//STOP EXIT//
/////////////


//////////////////////
//START SNEAK TUTORIAL

void SneakTutorial01(string asTimer)
{
	CreateSoundEntityAt("Entity","dognoise5","dogentrancesound","enemy_dog_attention.snt");
	
	CreateTimer("Sneak2v2", 0.5f, "SneakTutorial01v2",false);
}
void SneakTutorial01v2(string asTimer)
{
	AddMessageTrans("01_03_shafts", "TutorialSneak01");
	
	SetMessagesOverCallback("TutorialOver2");	
}
void TutorialOver2()
{
	AddMessage(
		Translate("01_03_shafts", "TutorialSneak02_1") +
		GetActionKeyString("Crouch") +
		Translate("01_03_shafts", "TutorialSneak02_2")
	);
				
	SetMessagesOverCallback("TutorialOver3");	
}
void TutorialOver3()
{
	CreateTimer("Sneak2", 4, "SneakTutorial02",false);
}
void SneakTutorial02(string asTimer)
{
	AddSubTitleTrans("01_03_shafts", "TutorialSneak03",11);
	AddSubTitleTrans("01_03_shafts", "TutorialSneak04",11);
}

void SneakTutorial03(string asTimer)
{
	AddSubTitleTrans("01_03_shafts", "TutorialSneak05",12);
}

//END SNEAK TUTORIAL
/////////////////////


/////////////////////////
//START LOCKED WORKSHOP//
void InteractLink03Disabled(string asEntity)
{	
	if(GetLocalVar("FirstAttempt") == 0)
	{
		AddMessageTrans("01_03_shafts", "EventNeedCodeDoorLocked");
		
		AddNotebookTask("01_03_need_code", "PersonalNotes", "01_03_need_code");
		
		PlayGuiSound("interact_locked_metal_door", 0.9f);
		
		SetLocalVar("FirstAttempt", 2);
	}
	else
	{
		AddMessageTrans("01_03_shafts", "EventNeedCodeDoorLocked02");	
		
		PlayGuiSound("interact_locked_metal_door", 0.9f);
	}
}
void InteractLink03Panel(string asEntity)
{
	StartNumericalPanel("code1",8,4,1,2,10.0f,"PanelCode");
}
void PanelCode(string asName, bool abCodeWasCorrect)
{
	if(abCodeWasCorrect)
	{	
		PlayGuiSound("door_code_correct", 0.8);
		
		RemoveNotebookTask("01_03_need_code");
		
		SetGameEntityActive("link03panel",false);
		SetGameEntityActive("link03disabled",false);
		SetGameEntityActive("link03",true);
	}
	else
	{
		PlayGuiSound("door_code_incorrect", 0.8);
		
		if(GetLocalVar("FirstAttempt") == 0)
		{
			AddMessageTrans("01_03_shafts", "EventNeedCode");
		
			AddNotebookTask("01_03_need_code", "PersonalNotes", "01_03_need_code");
			
			SetLocalVar("FirstAttempt", 1);
		}
		if(GetLocalVar("FirstAttempt") == 2)
		{
			AddMessageTrans("01_03_shafts", "EventNeedCode");
			
			SetLocalVar("FirstAttempt", 1);
		}
	}
		
}
//STOP LOCKED WORKSHOP//
////////////////////////


////////////////////////////////
//START DOG DISABLE AND ENABLE//
/*Disables & Enables the areas that disables dogs upon player enter
 */
void DogLeavesArea(string asParent, string asChild)
{
	for(int i=1;i<=5;i++)
	{
		SetGameEntityActive("disabledog"+i,false);
	}
	
	//AddSubTitle("Dog Left",3);
}
void DogEntersArea(string asParent, string asChild)
{
	for(int i=1;i<=5;i++)
	{
		SetGameEntityActive("disabledog"+i,true);
	}
	
	//AddSubTitle("Dog Entered",3);
}
/*Removes dog if out of sight for player and disables everything for this event
 */
void PlayerEntersDisableAreas(string asParent, string asChild)
{
	if(GetLocalVar("dogdisable") == 1)
	{
		for(int i=1;i<=2;i++)
		{
			SetGameEntityActive("dog0"+i,false);
		}
		for(int i=1;i<=5;i++)
		{
			SetGameEntityActive("disabledog"+i,false);
		}
		SetGameEntityActive("dogarea",false);
		
		CreateTimer("dogscares1", 30, "DogScare01",false);
		
		//AddSubTitle("Dog Disabled",3);
	}
}
void DogScare01(string asTimer)
{
	CreateSoundEntityAt("Entity","dognoise1","dogsound1","horror_dog");
	CreateTimer("dogscares2", 20, "DogScare02",false);
}
void DogScare02(string asTimer)
{
	CreateSoundEntityAt("Entity","dognoise2","dogsound2","horror_dog");
	CreateTimer("dogscares3", 30, "DogScare03",false);
}
void DogScare03(string asTimer)
{
	CreateSoundEntityAt("Entity","dognoise3","dogsound3","horror_dog");
	CreateTimer("dogscares4", 15, "DogScare04",false);
}
void DogScare04(string asTimer)
{
	CreateSoundEntityAt("Entity","dognoise4","dogsound4","horror_dog");
	CreateTimer("dogscares5", 40, "DogScare05",false);
}
void DogScare05(string asTimer)
{
	CreateSoundEntityAt("Entity","dognoise5","dogsound5","horror_dog");
	CreateTimer("dogscares6", 35, "DogScare06",false);
}
void DogScare06(string asTimer)
{
	CreateSoundEntityAt("Entity","dognoise6","dogsound6","horror_dog");
	CreateTimer("dogscares7", 20, "DogScare07",false);
}
void DogScare07(string asTimer)
{
	CreateSoundEntityAt("Entity","dognoise7","dogsound7","horror_dog");
	CreateTimer("dogscares8", 25, "DogScare08",false);
}
void DogScare08(string asTimer)
{
	CreateSoundEntityAt("Entity","dognoise8","dogsound8","horror_dog");
}
//STOP DOG DISABLE AND ENABLE//
///////////////////////////////


////////////////////////////
//START FOUND EXIT MESSAGE
void ExitMessage(string asParent, string asChild)
{
	AddSubTitleTrans("01_03_shafts", "EventFoundExit",5);
	
	SetGameEntityActive("exitmessage",false);
	
	RemoveNotebookTask("01_03_entrance_closed");
	
	AddNotebookTask("01_03_found_exit", "PersonalNotes", "01_03_found_exit");
	
	SetLocalVar("foundwayout", 1); //Triggers AddTask if player examines TNT.
}
//STOP FOUND EXIT MESSAGE//
///////////////////////////


//////////////////
//START DOG DIES//
void DogDie(string asEntity)
{
	if(StringContains(asEntity, "dog01"))
		SetLocalVar("whatdog", 2);
	if(StringContains(asEntity, "dog02"))
		SetLocalVar("whatdog", 1);
}
//STOP DOG DIES//
/////////////////


//////////////////////////////////////////////////////////
/*This function is run the FIRST time the map is loaded */
void OnStart()
{	
	//---- MAP INIT ------------
	SetMapGameNameTrans("SaveNames","level01_03_shafts");
	SetGlobalVar("old_mine_shafts_visited",1);
	if(GetGlobalVar("mine_entrance_visited")==1) ClearSavedMaps();
		
	//---- AREA INIT ------------
	SetAreaCustomIcon("entrance", "DoorLink");
	SetAreaCustomIcon("exitarea", "DoorLink");
	SetAreaCustomIcon("link03disabled", "DoorLink");
	
	SetGameEntityActive("triggerdog",false);
	SetGameEntityActive("triggerdog2",false);
	
	AddEntityCollideCallback("Enter","Player","triggerdog","Dog01");
	AddEntityCollideCallback("Enter","Player","triggerdog2","Dog201");
	AddEntityCollideCallback("Enter","Player","movedognorth","MoveDog01North");
	AddEntityCollideCallback("Enter","Player","movedogsouth","MoveDog01South");
	AddEntityCollideCallback("Enter","Player","exitarea","ExitEnter01");
	AddEntityCollideCallback("Enter","tnt1","dynamitesound","TntInHole01");
	//AddEntityCollideCallback("Leave","tnt1","dynamitehole","TntOutHole01");	
	AddEntityCollideCallback("Enter","Player","exitmessage","ExitMessage");
	for(int i=1;i<=2;i++)
		AddEntityCollideCallback("Leave","dog0"+i,"dogarea","DogLeavesArea");
	for(int i=1;i<=2;i++)
		AddEntityCollideCallback("Enter","dog0"+i,"dogarea","DogEntersArea");
	for(int i=1;i<=5;i++)
		AddEntityCollideCallback("Enter","Player","disabledog"+i,"PlayerEntersDisableAreas");
	
	SetupStickArea("stick1", 		//Name of the area
			false, 			//If it is possible to detach the object
			true, true,		//If the body should 1) moved 2)rotaed into place.
			false,			//If the centre must be in the area
			0.65,			//Time for setting pose, 0 = instant.
			"",
			"",			//Attach and Detach Sound
			"",
			"", 			//Attach and Detach particle system
			"OnAttach",
			"");		//Attach and Detach callback function
			
			
	//---- AUDIO INIT ------------
	StopSoundEntity("rumble",false);
	StopSoundEntity("howl1",false);
	StopSoundEntity("howl2",false);
	
	
	//---- ENEMY INIT ------------
	AddEnemyPatrolNode("dog01","Node117",2,"");
	AddEnemyPatrolNode("dog01","Node115",3,"");
	AddEnemyPatrolNode("dog01","Node31",1,"");
	AddEnemyPatrolNode("dog01","Node29",3,"");
	AddEnemyPatrolNode("dog01","Node35",2,"");
	AddEnemyPatrolNode("dog01","Node2",4,"");
	AddEnemyPatrolNode("dog01","Node37",3,"");
	AddEnemyPatrolNode("dog01","Node34",2,"");
	AddEnemyPatrolNode("dog01","Node26",4,"");
	
	AddEnemyPatrolNode("dog02","Node47",2,"");
	AddEnemyPatrolNode("dog02","Node42",3,"");
	AddEnemyPatrolNode("dog02","Node2",1,"");
	AddEnemyPatrolNode("dog02","Node33",3,"");
	AddEnemyPatrolNode("dog02","Node35",2,"");
	AddEnemyPatrolNode("dog02","Node327",4,"");
	
	
	//---- ENTITY INIT ------------
	SetGameEntityActive("link03",false);
	SetGameEntityActive("exitarea",false);
	SetGameEntityActive("tnt2",false);
	SetGameEntityActive("dynamitedeath",false);
	SetGameEntityActive("dog01",false);
	SetGameEntityActive("dog02",false);
	for(int i=1;i<=8;i++)
		SetGameEntityActive("rock"+i,false);
		
	SetDoorLocked("shaftdoor1", true);
	
	AddEntityCallback("OnBreak","shaftdoor1","DogDoorBreak");
	
	SetEnemyDeathCallback("dog01","DogDie");
	SetEnemyDeathCallback("dog02","DogDie");
	
	
	//---- LIGHT INIT ------------
	for(int i=1;i<=9;i++)
		SetLampLit("plamp"+i, false, false);
		
		
	//---- INTERACT INIT ------------
	AddEntityCallback("PlayerInteract","entrance","EntranceInteract01");
	AddEntityCallback("PlayerInteract","exitarea","ExitInteract01");
	AddEntityCallback("PlayerInteract","shaftdoor1","ShaftDoorInteract01");
	AddEntityCallback("PlayerInteract","link03panel","InteractLink03Panel");
	AddEntityCallback("PlayerInteract","link03disabled","InteractLink03Disabled");
	AddEntityCallback("PlayerExamine","tnt1","ExamineTNT");
	
	AddUseCallback("cottonstring", "tnt1", "StringOnBarrel01");
	AddUseCallback("cottonstringsoaked", "tnt1", "SoakedStringOnBarrel01");
	AddUseCallback("fuse", "tnt1", "FuseOnBarrel01");
	AddUseCallback("zippofire", "tnt1", "LightFuse01");
	AddUseCallback("oldminekey", "shaftdoor1", "ShaftDoorUnlock01");
	
	
	//---- LINK INIT ------------
	SetupLink(	"link02", //Link name
			"level01_05_office.dae", //New map
			"link01", //Positon on new map
			"door_metal_turnlock_open","door_metal_turnlock_close", //Stop and end sound.
			0.5f , 0.5f);//Fade out and in time (seconds).
			
	SetupLink(	"link03", //Link name
			"level01_06_workshop.dae", //New map
			"link01", //Positon on new map
			"door_metal_turnlock_open","door_metal_turnlock_close", //Stop and end sound.
			0.5f , 0.5f);//Fade out and in time (seconds).
			
	SetupLink(	"link04", //Link name
			"level01_04_old_storage.dae", //New map
			"link01", //Positon on new map
			"door_metal_turnlock_open","door_metal_turnlock_close", //Stop and end sound.
			0.5f , 0.5f);//Fade out and in time (seconds).
	
	
	//---- TEXT INIT ------------
	for(int i=1;i<=9;i++)
		SetGameEntityDescriptionOnceTrans("plamp"+i,"01_03_shafts","DescParaffin");
	if(HasItem("zippofire") == true)
	{
	for(int i=1;i<=9;i++)
		SetGameEntityDescriptionOnceTrans("plamp"+i,"01_03_shafts","DescParaffin02");
	}
	SetGameEntityDescriptionOnceTrans("tnt1","01_03_shafts","DescTNT01");
	SetGameEntityDescriptionOnceTrans("tnt2","01_03_shafts","DescTNT02");
	SetGameEntityDescriptionOnceTrans("cave1","01_03_shafts","DescCave01");
	SetGameEntityDescriptionOnceTrans("exitarea","01_03_shafts","DescExit");
	
	SetGameEntityDescriptionOnceTrans("descbox1","01_03_shafts","DescBox01");
	SetGameEntityDescriptionOnceTrans("descbox2","01_03_shafts","DescBox02");
	SetGameEntityDescriptionOnceTrans("descwheels1","01_03_shafts","DescWheels01");
	SetGameEntityDescriptionOnceTrans("descwheels2","01_03_shafts","DescWheels02");
	SetGameEntityDescriptionOnceTrans("descpile1","01_03_shafts","DescPile01");
	SetGameEntityDescriptionOnceTrans("descpile2","01_03_shafts","DescPile02");
	SetGameEntityDescriptionOnceTrans("descmachine","01_03_shafts","DescMachine");
	SetGameEntityDescriptionOnceTrans("descstuff","01_03_shafts","DescStuff");
	for(int i=9;i<=19;i++)
		SetGameEntityDescriptionOnceTrans("rock"+i,"01_03_shafts","DescRocks");
	for(int i=1;i<=6;i++)
		SetGameEntityDescriptionOnceTrans("canister"+i,"01_03_shafts","DescCanisters");	
	SetGameEntityDescriptionOnceTrans("link03panel","01_03_shafts","DescPanel");	
	
	//---- TIMER INIT ------------
	CreateTimer("shake1", 0.3f, "Shake01", false);
	if(GetGlobalVar("mine_entrance_visited")==1) CreateTimer("Sneak1", 11, "SneakTutorial01",false);
	
	
	//---- VARIABLE INIT ------------
	CreateLocalVar("savecheck", 0);
	CreateLocalVar("savecheck2", 0);
	CreateLocalVar("dogcheck", 0);
	CreateLocalVar("movedogcheck", 0);
	CreateLocalVar("keysave", 0);
	CreateLocalVar("workshopsave", 0);
	CreateLocalVar("FirstAttempt", 0);
	CreateLocalVar("dogdisable", 0);
	CreateLocalVar("whatdog", 0);
	CreateLocalVar("dogmessage", 0);
	CreateLocalVar("foundwayout", 0);
	CreateLocalVar("whatdog", 0);
	
	
	AutoSave();
}

/////////////////////////////////////////////////////
/*This function is run everytime the map is loaded */
void OnLoad()
{
	SetAmbientColor(0.087,0.102,0.082);	
	
	SetFogActive(true);
	SetFogCulling(true);
	SetFogProperties(10, //The distance at which fog start
			28,  //The distance at which fog has reach max intensity
			0.03,0.03,0.008 //Color of fog (r,g,b)
			);
	SetSkyboxActive(true);
	SetSkyboxColor(	0.03, 	//Red 
			0.03, 	//Green
			0.008, 	//Blue
			1); 	//Alpha, always set this to 1.
					
	PlayGameMusic("penumbra_music_E1_A4", 0.8f,0.4f,true, 0);
	
	PreloadSound("horror_deep_thump.snt");
	
	
	//---- ENTITY INIT ------------
	if(GetGlobalVar("HasExplosivesBook") == 1 && GetLocalVar("savecheck2") == 0)
	{
		if(GetLocalVar("dogcheck") == 0)
		{
			SetGameEntityActive("triggerdog",true);
		
			for(int i=1;i<=5;i++)
				SetGameEntityActive("disabledog"+i,true);

			SetLocalVar("dogcheck", 1);
		}
			
		SetLocalVar("savecheck2", 1);
		
		AutoSave();
	}
	if(GetGlobalVar("HasPickedZippo") == 1 && GetLocalVar("savecheck") == 0)
	{
		if(GetLocalVar("dogcheck") == 0)
		{
			SetGameEntityActive("triggerdog2",true);
		
			for(int i=1;i<=5;i++)
				SetGameEntityActive("disabledog"+i,true);
			
			SetLocalVar("dogcheck", 1);
		}
		
		SetLocalVar("savecheck", 1);
		
		AutoSave();
	}
	if(HasItem("oldminekey") == true && GetLocalVar("keysave") == 0)
	{
		SetLocalVar("keysave", 1);
		
		AutoSave();
	}
	if(HasItem("fuse") == true && GetLocalVar("workshopsave") == 0)
	{
		SetLocalVar("workshopsave", 1);
		
		AutoSave();
	}
	
}


///////////////////////////////////////
/*This function is run 60 times a sec*/
void OnUpdate()
{	

}


/////////////////////////////////////////////////////
/*This function is run everytime the map is unloaded */
void OnExit()
{
	StopGameMusic(0.3f, 0);
	
	FadeOutSoundEntity("rumble", 0.5f);
}